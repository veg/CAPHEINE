nextflow_process {

    name "Test Process HYPHY_BUSTED"
    script "modules/local/hyphy/busted/main.nf"
    process "HYPHY_BUSTED"

    test("Testing if BUSTED generates results") {

        when {
            process {
                """
                input[0] = [
                    [id: 'FBgn0000055_NT_033779.5_10_species_12_seqs-nodups'],
                    file(projectDir + '/tests/modules/local/hyphy/test_data/FBgn0000055_NT_033779.5_10_species_12_seqs-nodups.fasta', checkIfExists: true),
                    file(projectDir + '/tests/modules/local/hyphy/test_data/FBgn0000055_NT_033779.5_10_species_12_seqs-nodups.fasta.treefile', checkIfExists: true)
                ]
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                // Check the structure and key fields of the output
                { assert path(process.out.busted_json[0][1]).exists() },
                { assert path(process.out.busted_json[0][1]).text.contains('BUSTED') },
                // Check putput json for correctness
                { 
                    def json = new groovy.json.JsonSlurper().parseText(path(process.out.busted_json[0][1]).text)
                    
                    // Check if error sink rate class is included in entries
                    def rateDistTest = json.fits["Unconstrained model"]["Rate Distributions"].Test
                    assert rateDistTest.keySet().containsAll(["0", "1", "2", "3"]) 

                    // Check for correct BUSTED settings
                    def settings = json.analysis["settings"]
                    assert settings["error-sink"] == 1
                    assert settings["srv"] == "Yes"

                    // Check that BUSTED only ran on internal branches
                    def tested = json.tested["0"]
                    
                    // All test branches should contain 'node'
                    assert tested.findAll { k, v -> v == "test" }.every { k, v -> k.toLowerCase().contains("node") }
                    
                    // No background branches should contain 'node'
                    assert tested.findAll { k, v -> v == "background" }.every { k, v -> !k.toLowerCase().contains("node") }
                },
                // Verify metadata matches input
                { assert process.out.busted_json[0][0] == [id:'FBgn0000055_NT_033779.5_10_species_12_seqs-nodups'] },
                // Check version file exists and has correct format
                { assert path(process.out.versions[0]).exists() },
                { assert path(process.out.versions[0]).text.contains('hyphy:') }
            )
        }
    }

    test("BUSTED stub") {

        options "-stub"

        when {
            process {
                """
                input[0] = [
                    [id: 'FBgn0000055_NT_033779.5_10_species_12_seqs-nodups'],
                    file(projectDir + '/tests/modules/local/hyphy/test_data/FBgn0000055_NT_033779.5_10_species_12_seqs-nodups.fasta', checkIfExists: true),
                    file(projectDir + '/tests/modules/local/hyphy/test_data/FBgn0000055_NT_033779.5_10_species_12_seqs-nodups.fasta.treefile', checkIfExists: true)
                ]
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                // For stub test, just verify files exist with correct names
                { assert path(process.out.busted_json[0][1]).exists() },
                { assert path(process.out.busted_json[0][1]).toString().endsWith('.BUSTED.json') },
                { assert process.out.busted_json[0][0] == [id:'FBgn0000055_NT_033779.5_10_species_12_seqs-nodups'] },
                { assert path(process.out.versions[0]).exists() },
                { assert path(process.out.versions[0]).text.contains('hyphy:') }
            )
        }
    }
}
